#!/bin/csh -f
#
set s = "`pwd`"
set h = $s:t

set add = '>&'
set q = '"'
set pbsop = ""
set Ochk = ""
set clean = "clean"
set save = "full"
unalias mv
unalias cp
unalias rm

foreach o ( $* )
  if ( "$o" == "-ochk" ) then
    set Ochk = "check"
  else if ( "$o" == "-noclean" ) then
    set clean = "noclean"
  else if ( "$o" == "-brief" ) then
    set save = "brief"
  else
    set pbsop = "$pbsop $o"
  endif
end

foreach ee ( 1.000 )
    set d = V"$ee"

        if ( -e $d ) then
        if ( "$Ochk" != "check" || ! ( -e $d/OSZICAR ) || -z $d/OSZICAR ) then
        cd $d

vasppbs $pbsop >B-"$h"
set WORKDIR = "`pwd`"
set RUNDIR = '$tmpdir'
set VSPCMD = '$VSPCMD'

cat <<end.end >>B-"$h"
if ( !(-e $RUNDIR) ) mkdir $RUNDIR
cd $WORKDIR
unalias cp
unalias mv
cp -f KPOINTS  $RUNDIR
#cp -f POTCAR   $RUNDIR

if ( -e ../tplate/POTCAR ) then
  cp -f ../tplate/POTCAR $RUNDIR
else if ( -e ../tplate/POTCAR.gz ) then
  zcat ../tplate/POTCAR.gz   >$RUNDIR/POTCAR
else if ( -e ../../POTCAR.gz ) then
  zcat ../../POTCAR.gz   >$RUNDIR/POTCAR
endif

cp -f INCAR    $RUNDIR
cp -f POSCAR   $RUNDIR/POSCAR
cd $RUNDIR

$VSPCMD $add $RUNDIR/B-"$h".error

cp -f OSZICAR $WORKDIR/.
cp -f CONTCAR $WORKDIR/.

if ( "$save" == "full" ) then
  cp -f OUTCAR $WORKDIR/.
  cp -f DOSCAR $WORKDIR/.
else
  tail -500 OUTCAR >$WORKDIR/OUTCAR
endif

cp $RUNDIR/B-"$h".error $WORKDIR

if ( "$clean" == "clean" && "$WORKDIR" != "$RUNDIR" ) then
  set done = '\`tail -1 $WORKDIR/OSZICAR | grep E0 | wc -l | awk "{print $1}"\`'
  set run = "\$done"
  if ( "$RUNDIR":h != "$HOME" && $q\$run$q != $q"0"$q ) then
    rm $RUNDIR/* 
    rmdir $RUNDIR
  endif
endif
cd $WORKDIR
if ( -e OUTCAR ) then
    if ( ! -z OUTCAR ) then
	if ( -e OUTCAR.gz ) mv OUTCAR.gz OUTCAR.gz.old
	gzip -f OUTCAR
	if ( -e DOSCAR.gz ) mv DOSCAR.gz DOSCAR.gz.old
	gzip -f DOSCAR
    endif
endif
end.end

	qsub B-"$h"
        cd ..
	endif
	endif
end

